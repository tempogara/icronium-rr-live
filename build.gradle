plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'com.example'
version = '1.0.0'

repositories {
  mavenCentral()
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

javafx {
  version = '21.0.4'
  modules = [ 'javafx.controls' ]
}

dependencies {
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
}

application {
  mainClass = 'it.icron.icronium.live.MainApp'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.register('packageApp', Exec) {
	group = "distribution"
	description = "Crea IP Downloader.app per macOS"

	dependsOn build

	def javafxLib = configurations.runtimeClasspath
			.filter { it.name.contains("javafx") }
			.asPath

	commandLine = [
		"jpackage",
		"--type", "app-image",
		"--input", "${buildDir}/libs",
		"--dest", "${buildDir}/dist",
		"--name", "ICRONIUM-RR-Forworder",
		"--main-jar", jar.archiveFileName.get(),
		"--main-class", "it.icron.icronium.live.MainApp",
		"--java-options", "-Xmx512m",

		// IMPORTANTE: necessario per JavaFX
		"--module-path", javafxLib,

		// Include tutti i moduli disponibili (crypto inclusa)
		"--add-modules", "ALL-MODULE-PATH",

		// JVM options utili per HTTPS su Windows
		"--java-options", "-Dfile.encoding=UTF-8",
		"--java-options", "-Dhttps.protocols=TLSv1.3,TLSv1.2",
		"--java-options", "-Djdk.tls.client.protocols=TLSv1.3,TLSv1.2"
		
	]
}
