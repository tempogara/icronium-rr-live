plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'com.example'
version = '1.0.0'

repositories {
  mavenCentral()
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

javafx {
  version = '21.0.4'
  modules = [ 'javafx.controls' ]
}

dependencies {
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
}

application {
  mainClass = 'it.icron.icronium.live.MainApp'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.register('packageApp', Exec) {
	group = "distribution"
	description = "Crea IP Downloader.app per macOS"

	dependsOn build

	commandLine = [
		"jpackage",
		"--type", "app-image",
		"--input", "${buildDir}/libs",
		"--dest", "${buildDir}/dist",
		"--name", "IP Downloader",
		"--main-jar", "${jar.archiveFileName.get()}",
		"--main-class", "com.example.ipdownloader.MainApp",
		"--java-options", "-Xmx512m"
	]
}
